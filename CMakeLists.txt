cmake_minimum_required(VERSION 3.25)
project(StrawberryCodec)
include(FetchContent)


if (PROJECT_IS_TOP_LEVEL)
	FetchContent_Declare(StrawberryConfig
			GIT_REPOSITORY "https://github.com/StrawberryDan/Config.git"
			GIT_TAG "main")
	FetchContent_MakeAvailable(StrawberryConfig)
	include(StrawberryConfig)
endif()


find_package(PkgConfig REQUIRED)


pkg_check_modules(FFMPEG REQUIRED
		libavcodec
		libavdevice
		libavfilter
		libavformat
		libavutil
		libswresample
		libswscale
)


find_path(SODIUM_INCLUDE_DIR REQUIRED NAMES sodium.h
		HINTS /usr/local/include)
find_library(SODIUM_LIBRARY REQUIRED
		NAMES libsodium.a
		HINTS /usr/local/lib
)


if (NOT TARGET StrawberryCore AND EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/../Core AND IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/../Core)
	add_subdirectory(../Core ${CMAKE_CURRENT_BINARY_DIR}/Core)
endif ()


add_library(StrawberryCodec STATIC
		src/Codec/Audio/Decoder.cpp
		src/Codec/Audio/Decoder.hpp
		src/Codec/Audio/Encoder.cpp
		src/Codec/Audio/Encoder.hpp
		src/Codec/Audio/Frame.cpp
		src/Codec/Audio/Frame.hpp
		src/Codec/Audio/FrameFormat.cpp
		src/Codec/Audio/FrameFormat.hpp
		src/Codec/Audio/FrameResizer.cpp
		src/Codec/Audio/FrameResizer.hpp
		src/Codec/Audio/Mixer.cpp
		src/Codec/Audio/Mixer.hpp
		src/Codec/Audio/Playlist.cpp
		src/Codec/Audio/Playlist.hpp
		src/Codec/Audio/Resampler.cpp
		src/Codec/Audio/Resampler.hpp
		src/Codec/CodecParameters.hpp
		src/Codec/Constants.hpp
		src/Codec/MediaFile.hpp
		src/Codec/MediaStream.hpp
		src/Codec/Muxer.hpp
		src/Codec/Packet.hpp
		src/Codec/SodiumEncrypter.hpp
        src/Codec/MediaFile.cpp
        src/Codec/MediaStream.cpp
        src/Codec/Muxer.cpp
        src/Codec/Packet.cpp
        src/Codec/SodiumEncrypter.cpp
)


# FFMPEG Compile Flags
if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
	# Linker arguments generated by pkg-config for FFMPEG
	# pkg-config does not pass these arguments in nicely, so we do it manually.
	target_link_libraries(StrawberryCodec PUBLIC "-framework Security" "-framework VideoToolbox" "-framework AudioToolbox" "-framework CoreFoundation" "-framework CoreMedia" "-framework CoreVideo" "-framework CoreServices" "-framework CoreImage" "-framework CoreGraphics" "-framework VideoToolbox" "-framework OpenGL" "-framework Metal" "-framework Foundation" "-framework AppKit")
endif ()

target_include_directories(StrawberryCodec PUBLIC src ${FFMPEG_INCLUDE_DIRS} ${SODIUM_INCLUDE_DIR})
target_link_libraries(StrawberryCodec PUBLIC StrawberryCore ${FFMPEG_LINK_LIBRARIES} ${SODIUM_LIBRARY})
set_target_properties(StrawberryCodec PROPERTIES CXX_STANDARD 20)

if (NOT ${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
	target_link_options(StrawberryCodec PRIVATE "-static")
endif()


add_executable(StrawberryCodecTest test/Main.cpp)
target_link_libraries(StrawberryCodecTest PRIVATE StrawberryCodec)
set_target_properties(StrawberryCodecTest PROPERTIES CXX_STANDARD 20)
